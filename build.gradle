buildscript {
    repositories {
        jcenter() { 
            url = 'http://jcenter.bintray.com/' 
        } 
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            url = 'http://repository.jboss.org/nexus/content/groups/public'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
allprojects {
    repositories {
        jcenter() {
            url = 'http://jcenter.bintray.com/'
        }
        maven {
            url 'http://repository.jboss.org/nexus/content/groups/public' 
        }
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = System.getenv("BUILD_NUMBER") != null ? mod_version + "." + System.getenv("BUILD_NUMBER") : mod_version
group = "top.hookan.cocoa"
archivesBaseName = "Cocoa"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java/'
        }
        resources {
            srcDir "src/main/resources/"
        }
    }
    test {
        java {
            srcDir 'src/demo/java/'
        }
        resources {
            srcDir "src/demo/resources/"
        }
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLCorePlugin': project.group + '.core.CorePlugin',
                'FMLCorePluginContainsFMLMod': 'true'
    }
}

task testJar(type: Jar) {
    from sourceSets.test.output
    classifier = 'demo'
    manifest {
        attributes 'FMLCorePlugin': project.group + '.demo.core.Core',
                'FMLCorePluginContainsFMLMod': 'true'
    }
}

artifacts {
    archives deobfJar
    archives testJar
}

idea {
    module {
        sourceDirs += sourceSets.main.allSource.getSrcDirs()
        sourceDirs += sourceSets.test.allSource.getSrcDirs()
    }
}

minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = "run"
    mappings = "snapshot_20171003"

    replace "@version@", project.version
}

dependencies {
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': project.group + '.core.CorePlugin',
                'FMLCorePluginContainsFMLMod': 'true'
    }
}
